apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// load the signing configuration
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 27

    dataBinding.enabled true

    defaultConfig {
        // legacy name. Replace with de.jbamberger.fhgapp to match package
        applicationId "xyz.jbapps.vplan"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 22
        versionName '3.0.2'
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        testOptions {
            unitTests {
                includeAndroidResources = true
            }
            unitTests.all {
                jvmArgs '-noverify'
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/README.HTML'
        exclude 'readme.html'
        exclude 'license.html'
        exclude 'META-INF/eclipse.inf'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':utils')
    implementation project(':fhg-repository')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    kapt "com.android.databinding:compiler:$android_plugin_version"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:support-v13:$support_version"
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:preference-v14:$support_version"
    implementation "com.android.support:support-vector-drawable:$support_version"

    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation "android.arch.lifecycle:runtime:$arch_lifecycle_version"
    implementation "android.arch.lifecycle:extensions:$arch_lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$arch_lifecycle_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation "com.android.support.test:runner:$support_test_runner_version"
    androidTestImplementation "com.android.support.test:rules:$support_test_runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$support_espresso_version"

}