apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// load the signing configuration
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 27
    buildToolsVersion build_tools_version

    dataBinding.enabled true

    defaultConfig {
        // legacy name. Replace with de.jbamberger.fhgapp to match package
        applicationId "xyz.jbapps.vplan"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 24
        versionName '3.1.1'
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        testOptions {
            unitTests {
                includeAndroidResources = true
            }
            unitTests.all {
                jvmArgs '-noverify'
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/README.HTML'
        exclude 'readme.html'
        exclude 'license.html'
        exclude 'META-INF/eclipse.inf'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':fhg-repository')

    implementation lib_kotlin_std_lib

    kapt apt_databinding_compiler

    implementation lib_support_annotations
    implementation lib_support_appcompat_v7
    implementation lib_support_v4
    implementation lib_support_v13
    implementation lib_support_design
    implementation lib_support_cardview_v7
    implementation lib_support_preference_v14
    implementation lib_support_vector_drawable
    implementation lib_support_constraint_layout

    implementation play_oss_licenses

    kapt apt_glide

    implementation lib_timber

    implementation lib_lifecycle_runtime
    implementation lib_lifecycle_extensions
    kapt apt_lifecycle

    implementation lib_dagger
    implementation lib_dagger_android
    implementation lib_dagger_android_support
    kapt apt_dagger
    kapt apt_dagger_android

    testImplementation lib_jsr305
    testImplementation lib_junit
    testImplementation lib_assertj
    testImplementation lib_robolectric
    testImplementation lib_mockito
    testImplementation lib_arch_testing
    testImplementation lib_room_testing
    testImplementation lib_paging_testing

    androidTestImplementation lib_jsr305
    androidTestImplementation lib_support_test_runner
    androidTestImplementation lib_support_test_rules
    androidTestImplementation(lib_espresso_core) {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
}